IF
	Clicked([ANYONE])
	GlobalGT("AC#FP_AC#FP7_Solved","GLOBAL",0)
THEN
	RESPONSE #100
		DisplayString(Myself,@1271)  // ~The water is calm again. There is nothing more to see in this pool.~
END

IF
	Clicked([ANYONE])
	Global("AC#FP_AC#FP7_Entered","GLOBAL",1)
THEN
	RESPONSE #100
		DisplayString(Myself,@1277)  // ~Only one person at a time can immerse into the water.~
END

IF
	Clicked([ANYONE])
	Range(LastTrigger,10)
	Global("Mindpool04","AC#FP2",0)
THEN
	RESPONSE #100
		DisplayString(Myself,@1275)  // ~There is no key in this pool, but you could still delve beneath the water and see what awaits you there.~
		SetGlobal("Mindpool04","AC#FP2",1)
END

IF
	Clicked([ANYONE])
	Range(LastTrigger,10)
	Global("Mindpool04","AC#FP2",1)
THEN
	RESPONSE #100
		ClearAllActions()
		StartCutSceneMode()
		SetGlobal("AC#FP_AC#FP7_Entered","GLOBAL",1)
		ActionOverride(Player1,MakeUnselectable(99999999))
		ActionOverride(Player2,MakeUnselectable(99999999))
		ActionOverride(Player3,MakeUnselectable(99999999))
		ActionOverride(Player4,MakeUnselectable(99999999))
		ActionOverride(Player5,MakeUnselectable(99999999))
		ActionOverride(Player6,MakeUnselectable(99999999))
		ActionOverride(LastTrigger,MakeUnselectable(0))
		MoveViewObject(LastTrigger,INSTANT)
		ActionOverride(LastTrigger,FaceObject("Mindpool04"))
		DisplayStringNoName(Myself,@1272) //~You submerge your head below the water.~
		FadeToColor([20.0],0)
		Wait(1)
		PlaySound("EFF_M63")
		ActionOverride(LastTrigger,LeaveAreaLUAPanic("AC#FP7","",[731.874],N))
		ActionOverride(LastTrigger,LeaveAreaLUA("AC#FP7","",[731.874],N))
		MultiPlayerSync()
		MoveViewObject(LastTrigger,INSTANT)
		FadeFromColor([15.0],0)
		EndCutSceneMode()
END

IF
	Clicked([ANYONE])
	!Range(LastTrigger,10)
THEN
	RESPONSE #100
		DisplayString(Myself,@1276)  
END

